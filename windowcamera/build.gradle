ext {
    applicationId = 'windowcamera'
    versionName = '0.6'
}

apply from: rootProject.file('util.gradle.kts')
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.gms.google-services'
//apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion compile_sdk_version
    defaultConfig {
        applicationId "com.ebnbin.${project.applicationId}"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode util.versionCode
        versionName project.versionName

        ndk.abiFilters 'armeabi-v7a'/*, 'x86'*/
    }
    signingConfigs {
        release {
            storeFile rootProject.file('gitignore/ebnbin.jks')
            storePassword util.localProperties['signingPassword']
            keyAlias 'ebnbin'
            keyPassword util.localProperties['signingPassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles util.proguardFiles
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'flavor'
    productFlavors {
        github.dimension 'flavor'
        google.dimension 'flavor'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    viewBinding {
        enabled true
    }
    applicationVariants.all { applicationVariant ->
        applicationVariant.outputs.all {
            outputFileName = "$applicationId" +
                "-$versionName" +
                "-${applicationVariant.flavorName}" +
                "-${applicationVariant.buildType.name}.apk"
        }
    }

    sourceSets.main.res.srcDirs(
        'src/main/res-viewer',
        'src/main/res-album',
        'src/main/res-old'
    )
}

dependencies {
    implementation project(':ebapp')

    api 'com.github.chrisbanes:PhotoView:2.3.0' // https://github.com/chrisbanes/PhotoView
    api 'com.google.android.exoplayer:exoplayer:2.11.1' // https://github.com/google/ExoPlayer




    api 'io.reactivex.rxjava2:rxjava:2.2.12' // https://github.com/ReactiveX/RxJava
    api 'io.reactivex.rxjava2:rxandroid:2.1.1' // https://github.com/ReactiveX/RxAndroid
    api 'com.squareup.retrofit2:adapter-rxjava2:2.7.0'
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46' // https://github.com/CymChad/BaseRecyclerViewAdapterHelper
}
