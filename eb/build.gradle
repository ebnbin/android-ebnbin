apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def buildTimestamp = System.currentTimeMillis()

def localProperties = new Properties()
localProperties.load(new FileReader(rootProject.file('local.properties')))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        buildConfigField 'long', 'BUILD_TIMESTAMP', "${buildTimestamp}L"
        buildConfigField 'String', 'GITHUB_API_TOKEN', "\"${localProperties['githubApiToken']}\""
        setConsumerProguardFiles fileTree('proguard/')
    }
    buildTypes {
        release {
            manifestPlaceholders = [firebaseCrashlyticsCollectionEnabled: 'true']
        }
        prerelease {
            debuggable true
            manifestPlaceholders = [firebaseCrashlyticsCollectionEnabled: 'false']
        }
        debug {
            manifestPlaceholders = [firebaseCrashlyticsCollectionEnabled: 'false']
        }

    }
    flavorDimensions 'flavor'
    productFlavors {
        github.dimension 'flavor'
        coding.dimension 'flavor'
        google.dimension 'flavor'
        coolapk.dimension 'flavor'
        qq.dimension 'flavor'
        uc.dimension 'flavor'
        mi.dimension 'flavor'
        huawei.dimension 'flavor'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    resourcePrefix "eb_"
    dataBinding {
        enabled true
    }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion" // https://github.com/JetBrains/kotlin
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2' // https://github.com/Kotlin/kotlinx.coroutines
    api 'androidx.appcompat:appcompat:1.1.0' // https://dl.google.com/dl/android/maven2/index.html
    api 'androidx.core:core-ktx:1.1.0'
    api 'androidx.preference:preference-ktx:1.1.0'
    api 'com.google.android.material:material:1.1.0-beta02'
    api 'com.google.firebase:firebase-analytics:17.2.1' // https://firebase.google.com/docs/android/setup
    api 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    api 'cat.ereza:customactivityoncrash:2.2.0' // https://github.com/Ereza/CustomActivityOnCrash
    api 'org.greenrobot:eventbus:3.1.1' // https://github.com/greenrobot/EventBus
    api 'com.google.code.gson:gson:2.8.5' // https://github.com/google/gson
    api 'io.reactivex.rxjava2:rxjava:2.2.12' // https://github.com/ReactiveX/RxJava
    api 'io.reactivex.rxjava2:rxandroid:2.1.1' // https://github.com/ReactiveX/RxAndroid
    api 'com.squareup.okhttp3:okhttp:3.14.1' // https://github.com/square/okhttp
    api 'com.squareup.retrofit2:retrofit:2.6.1' // https://github.com/square/retrofit
    api 'com.squareup.retrofit2:converter-gson:2.6.1'
    api 'com.squareup.retrofit2:adapter-rxjava2:2.6.1'
    api 'com.github.bumptech.glide:glide:4.9.0' // https://github.com/bumptech/glide
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.46' // https://github.com/CymChad/BaseRecyclerViewAdapterHelper
    api 'com.github.chrisbanes:PhotoView:2.3.0' // https://github.com/chrisbanes/PhotoView
    api 'com.shuyu:gsyVideoPlayer-java:7.0.1' // https://github.com/CarGuo/GSYVideoPlayer
    api 'com.shuyu:GSYVideoPlayer-exo2:7.0.1'
    api 'com.shuyu:gsyVideoPlayer-armv7a:7.0.1'
//    api 'com.shuyu:gsyVideoPlayer-x86:7.0.1'
}
