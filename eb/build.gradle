apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        def files = file('proguard/').listFiles()
        if (files != null) consumerProguardFiles files
        buildConfigField 'long', 'BUILD_TIMESTAMP', "${System.currentTimeMillis()}L"
    }
    buildTypes {
        release {
            manifestPlaceholders = [firebaseCrashlyticsCollectionEnabled: true]
        }
        prerelease {
            initWith release
            debuggable true
            manifestPlaceholders = [firebaseCrashlyticsCollectionEnabled: false]
        }
        debug {
            manifestPlaceholders = [firebaseCrashlyticsCollectionEnabled: false]
        }
    }
    flavorDimensions 'flavor'
    productFlavors {
        github.dimension 'flavor'
        google.dimension 'flavor'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    resourcePrefix 'eb'
    dataBinding {
        enabled = true
    }
}

dependencies {
    // https://github.com/JetBrains/kotlin
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    // https://firebase.google.com/docs/android/setup
    api 'com.google.firebase:firebase-analytics:17.2.1'
    // https://firebase.google.com/docs/crashlytics/get-started?platform=android
    api 'com.crashlytics.sdk.android:crashlytics:2.10.1'
}
