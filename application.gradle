apply from: rootProject.file('util.gradle.kts')
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'

android {
    compileSdkVersion versions.compile_sdk_version
    defaultConfig {
        applicationId "com.ebnbin.${project.applicationId}"
        minSdkVersion versions.min_sdk_version
        targetSdkVersion versions.target_sdk_version
        versionCode rootProject.util.versionCode
        versionName project.versionName
    }
    signingConfigs {
        release {
            storeFile rootProject.file('gitignore/ebnbin.jks')
            storePassword rootProject.util.localProperties['signingPassword']
            keyAlias 'ebnbin'
            keyPassword rootProject.util.localProperties['signingPassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles rootProject.util.proguardFiles
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'flavor'
    productFlavors {
        github.dimension 'flavor'
        google.dimension 'flavor'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    dataBinding {
        enabled true
    }
    androidExtensions {
        experimental = true
    }
    applicationVariants.all { applicationVariant ->
        applicationVariant.outputs.all {
            outputFileName = "$applicationId" +
                    "-$versionName" +
                    "-${applicationVariant.flavorName}" +
                    "-${applicationVariant.buildType.name}.apk"
        }
    }
}

dependencies {
    implementation project(':eb-app')
}
